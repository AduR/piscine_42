#!/usr/bin/perl
#
# Copyright 2014 Pierre Mavro <deimos@deimos.fr>
# Copyright 2014 Vivien Didelot <vivien@didelot.org>
#
# Licensed under the terms of the GNU GPL v3, or any later version.
#
# This script is meant to use with i3blocks. It parses the output of the "acpi"
# command (often provided by a package of the same name) to read the status of
# the battery, and eventually its remaining time (to full charge or discharge).
#
# The color will gradually change for a percentage below 85%, and the urgency
# (exit code 33) is set if there is less that 5% remaining.

use strict;
use warnings;
use utf8;

my $acpi;
my $status;
my $percent;
my $full_text;
my $short_text;

# read the first line of the "acpi" command output
open (ACPI, 'acpi -b | grep -v unavailable |') or die "Can't exec acpi: $!\n";
$acpi = <ACPI>;
close(ACPI);

# fail on unexpected output
if ($acpi !~ /: (\w+), (\d+)%/) {
    die "$acpi\n";
}

$status = $1;
$percent = $2;
$full_text = "$percent%";

if ($status eq 'Discharging') {
    $full_text .= ' DIS';
} elsif ($status eq 'Charging') {
    $full_text .= ' CHR';
}

$short_text = $full_text;

if ($acpi =~ /(\d\d:\d\d):/) {
    $full_text .= " ($1)";
}

# print text
print "$full_text\n";
print "$short_text\n";

# consider color and urgent flag only on discharge
if ($status eq 'Discharging') {
    # color
    if ($percent < 10) {
        print "#FF0000\n";  # red
    } elsif ($percent < 20) {
        print "#FFAE00\n";  # orange
    } elsif ($percent < 30) {
        print "#CFC600\n";  # yellow
    }

    # notification
    if ($percent < 5) {
        system("notify-send", "-u", "critical", "Battery is critical !");
    } elsif ($percent == 10) {
        system("notify-send",  "Battery is low ($percent%)");
    } elsif ($percent == 20) {
        system("notify-send",  "Battery is low ($percent%)");
    }

    if ($percent < 5) {
        exit(33);
    }
}
else {
    if ($percent > 98) {
        print "#00C000\n";  # green
        #system("notify-send",  "Battery is full");
    }
}

exit(0);
